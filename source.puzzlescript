title BFS
author vexorian

(verbose_logging)
enable_level_select


(
zlib License

Copyright (c) 2021, Victor Hugo Soliz Kuncar

This software is provided 'as-is', without any express or implied
warranty. In no event will the authors be held liable for any damages
arising from the use of this software.

Permission is granted to anyone to use this software for any purpose,
including commercial applications, and to alter it and redistribute it
freely, subject to the following restrictions:

1. The origin of this software must not be misrepresented; you must not
   claim that you wrote the original software. If you use this software
   in a product, an acknowledgment in the product documentation would be
   appreciated but is not required.
2. Altered source versions must be plainly marked as such, and must not be
   misrepresented as being the original software.
3. This notice may not be removed or altered from any source distribution.

)



========
OBJECTS
========

Background
Black

Wall
White

Player
Blue

Monster
Red

BFSU
transparent

BFSD
transparent

BFSR
transparent

BFSL
transparent


=======
LEGEND
=======

. = Background
P = Background and Player
# = Background and Wall
x = Background and Monster

BFS = BFSU or BFSD or BFSL or BFSR


=======
SOUNDS
=======

================
COLLISIONLAYERS
================

Background
Player, Wall, Monster
BFS


======
RULES
======
[ action Player] -> win

[ > Player | no Wall no Monster ] -> [ | Player ]
[ Player ] -> [ stationary Player ]

[BFS] -> []
[ Player ] -> [ Player random BFS ]

startloop

  [ action BFS ] -> [ stationary BFS ]
  down [ no BFS no Wall no Player | stationary BFS no Monster ] -> [ action BFSD | stationary BFS ]
  up   [ no BFS no Wall no Player | stationary BFS no Monster ] -> [ action BFSU | stationary BFS ]
  left [ no BFS no Wall no Player | stationary BFS no Monster ] -> [ action BFSL | stationary BFS ]
  right[ no BFS no Wall no Player | stationary BFS no Monster ] -> [ action BFSR | stationary BFS ]

endloop

[ BFSU Monster ] -> [ up    Monster ]
[ BFSD Monster ] -> [ down  Monster ]
[ BFSL Monster ] -> [ left  Monster ]
[ BFSR Monster ] -> [ right Monster ]

late [ Monster | Player ] -> [ Monster | ] 




==============
WINCONDITIONS
==============

=======
LEVELS
=======

level_select_point

#######
#X....#
#.....#
#.....#
#.....#
#....P#
#######

level_select_point

#########
#X.#....#
#..#.#..#
#..#.#..#
#..#.#..#
#....#.P#
#########

level_select_point

###########
#.......x.#
#..#.####.#
#..#...#..#
#..#p#.#..#
#....#....#
#....####.#
#.#.....#.#
#x####.x#.#
#.........#
###########

level_select_point

#############
#........#..#
#..####x##..#
#.....###...#
####....#.x.#
#..#....#...#
#.x#..p.###.#
#..#........#
#.##..###...#
#.##.##.#...#
#....#.x.#..#
#....#...#..#
#....#......#
#############

level_select_point

#################
#...............#
#..x.x..........#
#..............##
#.x............##
#.#####..###.#.##
#........#...#..#
#.##.######..#..#
#...........##..#
#.############..#
#..#...#........#
#..#.#.#........#
#..#.#........p.#
#....#..........#
#################

level_select_point

#####################
#...................#
#.#..####....x..#...#
#.#..#..#..####.##..#
#.#.....#.....#.##..#
#.####..#.###.#.##..#
#...#...#.....#.##..#
#..x#.#.##..#.#.....#
#.#.#.#..#..#.#.###.#
#.#...#.....#.#.....#
#.#.........#.#.###.#
#.#.........#...###.#
#.###########.#.....#
#.............#..p..#
#.............#.###.#
#..############.###.#
#...................#
#####################
